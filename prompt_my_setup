function prompt_my_precmd {
  git-info

  # ruby info
  _ruby='%F{1}Ruby:%F{9}'
  _node='%F{3}NodeJS:%F{11}'

  _ruby+=$(asdf current ruby &> /dev/null && asdf current ruby | sed 's/ .*$//')
  _ruby_gemset=''
  _node+=$(asdf current nodejs &>/dev/null && asdf current nodejs | sed 's/ .*$//')

  _ruby+='%f'
  _node+='%f'

  # last commit

  _git=""

  if [ -d .git ]; then

    _last_commit=$(git log -1 --pretty='%s')

    if [[ "$_last_commit" =~ [Ww][Ii][Pp] ]]; then
      _git+="%F{1} WIP!!! "
    fi

  fi

}

function prompt_my_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_my_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{green}(%f$(coalesce "%b" "%p" "%c")${git_info[rprompt]}%s%F{green})%f' \
    'rprompt' '%C%D'



  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _colors=(
      "%F{81}"  # Turquoise
      "%F{208}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Define prompts.
  PROMPT='
%F{15}In ${_colors[3]}%~%f${(e)git_info[prompt]} $_ruby $_git $_node
%B%F{1}❯%F{3}❯%F{2}❯%f%b  '

  RPROMPT=''
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

prompt_my_setup "$@"
